#! /bin/sed -f
#polish.sedf9: UNDO troff cmds, U.S.A., EQ, 1,987, endofline em
/^\.\\"/!{
s/^\.\(.. \)\\-/.\1-/
s/^\.\([A-Z] \)\\-/.\1-/
/^\.[^H].*\\(ct/s/\\(ct/c/g
/^\.G1/,/^\.G2/!{
/^\.PS/,/^\.PE/!{
/^\.EQ/,/^\.EN/!{
        /^[^.]/{
                s/,,/,/g
                s/delim(\\(lq)/delim(``)/
                s/^Rather, than/Rather than/
                /-[ah]/{
                s/\([a-z]\)-and /\1- and /g
                s/\([a-z]\)-and$/\1- and/
                s/have-to-have/have to have/g
                }
                /(pl/{
                s/\\s\\(pl/\\s+/g
                s/\\s\\\^\\(pl\\\^/\\s+/g
                }
                /U\.\\\^[SK]/{
                s/^U\.\\\^S\.\\\^A\.\([^A-Z]\)/U.S.A.\1/
                s/^U\.\\\^S\.\\\^A\.$/U.S.A./
                s/\([^.]\)U\.\\\^S\.\\\^A\.\([^A-Z]\)/\1U.S.A.\2/g
                s/\([^.]\)U\.\\\^S\.\\\^A\.$/\1U.S.A./
                s/^U\.\\\^\([SK]\)\.\([^A-Z]\)/U.\1.\2/
                s/^U\.\\\^\([SK]\)\.$/U.\1./
                s/\([^.]\)U\.\\\^\([SK]\)\.\([^A-Z]\)/\1U.\2.\3/g
                s/\([^.]\)U\.\\\^\([SK]\)\.$/\1U.\2./
                }
                /[pn]\.\\/{
                s/\([ (p]\)p\.\\\& \([0-9A-Z]\)/\1p.\\^\2/g
                s/^p\.\\\& \([0-9A-Z]\)/p.\\^\1/
                s/fn\.\\\& \([0-9]\)/fn.\\^\1/g
                }
                s/ vs / \\f1v.\\\&\\fP /g
        }
        /^\.TS/,/^\.TE/!{
        /^\.nf/,/^\.fi/!{
                /^[^.]/{
                        /\\(em$/{
                                N
                                s/\n//
                        }
                }
        }
        }
}
/\\/{
s/size \\-\([0-9]\)/size -\1/g
s/\\(\\(/\\(/g
s/\\(\\(/\\(/g
}
/1,9/{
s/^1,9\([0-9][0-9][^0-9]\)/19\1/g
s/\([^0-9]\)1,9\([0-9][0-9][^0-9]\)/\119\2/g
s/\([^0-9]\)1,9\([0-9][0-9][^0-9]\)/\119\2/g
s/\([^0-9]\)1,9\([0-9][0-9]\)$/\119\2/
s/\(\\[sf][0-9]\)1,9\([0-9][0-9][^0-9]\)/\119\2/g
s/\(\\s[+-][0-9]\)1,9\([0-9][0-9][^0-9]\)/\119\2/g
}
/2,0/{
s/^2,0\([0-9][0-9][^0-9]\)/20\1/g
s/\([^0-9]\)2,0\([0-9][0-9][^0-9]\)/\120\2/g
s/\([^0-9]\)2,0\([0-9][0-9][^0-9]\)/\120\2/g
s/\([^0-9]\)2,0\([0-9][0-9]\)$/\120\2/
}
}
}
/^\.TS/,/^\.TE/{
        /\\s-2(/{
        s/\([   ^]\)\\s-2(\(ii*\))\\s+2\1/\1(\2)\1/g
        s/\([   ^]\)\\s-2(\(ii*\))\\s+2\1/\1(\2)\1/g
        s/\([   ^]\)\\s-2(\(ii*\))\\s+2$/\1(\2)/
        s/^\\s-2(\(ii*\))\\s+2\([       ^]\)/(\1)\2/
        }
}
}
