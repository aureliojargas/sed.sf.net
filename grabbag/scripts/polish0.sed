#! /bin/sed -f
#polish.sedf0: endline, endsentence, %, vis-a-vis, em dash, EQ, C.A.P.S.

/^\.\\"/!{
s/[     ][      ]*$//
/^\.G1/,/^\.G2/!{
/^\.PS/,/^\.PE/!{
        s/" " /"" /g
/^\.EQ/,/^\.EN/!{
        /^\.TS/,/^\.TE/!{
        /^\.nf/,/^\.fi/!{
                /^[^.]/{
                        /[.?!]..*[A-Z]/{
                        s/\.   *\([\A-Z]\)/.\
\1/g
                        s/\.)   *\([\A-Z]\)/.)\
\1/g
                        s/\([?!]\)  *\([\A-Z]\)/\1\
\2/g
                        s/\([.?!]\)  *\([[(]\)\([\A-Z]\)/\1\
\2\3/g
                        s/\([^ds]\)\.)  *\([\A-Z]\)/\1.)~\
\2/g
                        s/\([^ds]\)\."  *\([\A-Z]\)/\1.\\(rq~\
\2/g
                        s/\([^ds]\)\.]  *\([\A-Z]\)/\1.]~\
\2/g
                        s/)\. \([A-Z]\)/).\
\1/g
                        }
                        / -$/{
                                N
                                s/ -\n/\\(em/
                        }
                        /[0-9]/{
                        /\([0-9]\) per$/{
                                N
                                s/ per\ncent/%/
                        }
                        /\([0-9]\)$/{
                                N
                                s/\nper cent/%/
                                s/\npercent\([^ai]\)/%\1/
                        }
                        }
                }
        }
        /^\.H [123]/{
                n
                /^\.P$/d
                }
        /^[^.]/{
                s/\([A-Z]\)\.\([A-Z]\)/\1.\\^\2/g
                s/\([A-Z]\)\.\([A-Z]\)/\1.\\^\2/g
        }
        /[`!$]/!{
                /^\.nf/,/^\.fi/!{
                /^[^ ][^ ]*  /!{
                        /^[^.]/s/\([^sf+][^?;:]\)  /\1 /g
                }
                }
        }
        }
        /[`!$]/!{
                /^[^.]/{
                        /-/{
                        s/ - /\\(em/g
                        s/\([0-9]\)- \([a-zA-Z]\)/\1\- \2/g
                        }
                }
        }
        /^[^.']/{
                s/percent\([^ai]\)/per cent\1/g
                /[0-9]c/{
                s/\([0-9]\)c /\1\\(ct /g
                s/\([0-9]\)c$/\1\\(ct/
                }
                /--./{
                s/ -- /\\(em/g
                s/--\(.\)/\\(em\1/g
                }
        }
        /^\.nf/,/^\.fi/!{
                /^[^.]/{
                        /vis/{
                        / vis a$/{
                                N
                                s/ a\nvis/-\\o'a\\(ga'-vis/
                        }
                        / vis$/{
                                N
                                s/\na vis/-\\o'a\\(ga'-vis/
                        }
                        }
                        /--$/{
                        / --$/{
                                N
                                s/ --\n/\\(em/
                        }
                        /[a-zA-Z0-9]--$/{
                                N
                                s/--\n/\\(em/
                        }
                        }
                }
        }
}
}
}
}
